## Build lammps inputs for random POPC-water system

proc assembly {} {

    package require cg 

    ## Load our PDB of our already
    ## coarse grained 20:1 water:popc
    ## system generated by packmol
    set molid [mol new popc-water.pdb]

    ## Read the parameters
    cg readparam ../common/par_all1_sdk.json
    cg readtop   ../common/top_all1_sdk.json

    ## set bonds, angles and apply
    ## their types
    cg setbonds
    cg setangles
    cg reanalyzemol

    ## Set our periodic cell dimensions
    set sel [atomselect $molid "all"]
    set box [vecinvert [vecsub {*}[measure minmax $sel -withradii]]]
    molinfo $molid set {a b c} $box
    $sel delete

    ## replicate the system 3x3x3, bonding, angle
    ## and typing information is carried over
    set newmol [::TopoTools::replicatemol $molid 3 3 3]

    ## write out the lammps input files and
    ## a psf for future use
    cg -molid $newmol writelammpsdata popc-water.data full
    cg -molid $newmol writelammpsparam popc-water.param both
    animate write psf popc-water.psf
}

if {1} {assembly}
